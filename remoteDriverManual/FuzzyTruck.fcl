FUNCTION_BLOCK FuzzyTruck	// Block definition

VAR_INPUT				// Define input variables
	x : REAL;
	angle: REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	wheelAngle : REAL;
END_VAR

FUZZIFY x			// Fuzzify input variable 'x': {'LE', 'LC', 'CE', 'RC', 'RI'}
	TERM LE := (0.00, 1)	(0.10, 1)	(0.10, 1) 	(0.35, 0); // Left
	TERM LC := (0.30, 0)	(0.40, 1) 	(0.40, 1)	(0.50, 0); // Left-Center
	TERM CE := (0.45, 0)	(0.50, 1) 	(0.50, 1)	(0.55, 0); // Center
	TERM RC := (0.50, 0)	(0.60, 1) 	(0.60, 1)	(0.70, 0); // Right-Center
	TERM RI := (0.65, 0)	(0.90, 1) 	(0.90, 1)	(1.00, 1); // Right
END_FUZZIFY

FUZZIFY angle			// Fuzzify input variable 'angle': {'LE', 'CL', 'CE', 'CR', 'RI'}
	TERM RB := (260, 0)		(315, 1)	(315, 1) 	(360, 0); // Right-Bellow
	TERM RU := (0, 0)		(35.0, 1) 	(35.0, 1)	(60.0, 0); // Right-Upper
	TERM RV := (45.0, 0)	(67.5, 1) 	(67.5, 1)	(90.0, 0); // Right-Vertical
	TERM VE := (80.0, 0)	(90.0, 1) 	(90.0, 1)	(100, 0); // Vertical
	TERM LV := (90.0, 0)	(112.5, 1) 	(112.5, 1)	(135, 0); // Left-Vertical
	TERM LU := (120, 0)		(155, 1) 	(155, 1)	(190, 0); // Left-Upper
	TERM LB := (170, 0)		(225, 1) 	(225, 1)	(280, 0); // Left-Bellow
END_FUZZIFY

DEFUZZIFY wheelAngle	// Defuzzify output variable 'wheelAngle' : {'NB', 'NM', 'NS', 'ZE', 'PS', 'PM', 'PB'}
	TERM NB := (-1.00, 1) 	(-0.50, 0) 	(-0.50, 0) 	(-0.50, 0); // Negative Big
	TERM NM := (-0.83, 0) 	(-0.50, 1) 	(-0.50, 1) 	(-0.16, 0); // Negative Medium
	TERM NS := (-0.40, 0)	(-0.20, 1) 	(-0.20, 1) 	(0.00, 0); 	// Negative Small
	TERM ZE := (-0.16, 0) 	(0.00, 1) 	(0.00, 1) 	(0.16, 0);	// Zero
	TERM PS := (0.00, 0) 	(0.20, 1) 	(0.20, 1) 	(0.40, 0);	// Positive Small
	TERM PM := (0.16, 0) 	(0.50, 1) 	(0.50, 1) 	(0.83, 0);	// Positive Medium
	TERM PB := (0.50, 0)	(1.00, 1) 	(1.00, 1) 	(1.00, 1);  // Positive Big
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;
	OR : MAX;
    
	RULE 1  : IF x IS LE AND angle IS RB THEN wheelAngle IS PB;
	RULE 2  : IF x IS LE AND angle IS RU THEN wheelAngle IS PB;
	RULE 3  : IF x IS LE AND angle IS RV THEN wheelAngle IS PB;
	RULE 4  : IF x IS LE AND angle IS VE THEN wheelAngle IS PM;
	RULE 5  : IF x IS LE AND angle IS LV THEN wheelAngle IS PM;
	RULE 6  : IF x IS LE AND angle IS LU THEN wheelAngle IS PS;
	RULE 7  : IF x IS LE AND angle IS LB THEN wheelAngle IS NS;
	
	RULE 8  : IF x IS LC AND angle IS RB THEN wheelAngle IS NB;
	RULE 9  : IF x IS LC AND angle IS RU THEN wheelAngle IS PB;
	RULE 10 : IF x IS LC AND angle IS RV THEN wheelAngle IS PB;
	RULE 11 : IF x IS LC AND angle IS VE THEN wheelAngle IS PM;
	RULE 12 : IF x IS LC AND angle IS LV THEN wheelAngle IS ZE;
	RULE 13 : IF x IS LC AND angle IS LU THEN wheelAngle IS NM;
	RULE 14 : IF x IS LC AND angle IS LB THEN wheelAngle IS NB;
	
	RULE 15 : IF x IS CE AND angle IS RB THEN wheelAngle IS NB;
	RULE 16 : IF x IS CE AND angle IS RU THEN wheelAngle IS PM;
	RULE 17 : IF x IS CE AND angle IS RV THEN wheelAngle IS PS;
	RULE 18 : IF x IS CE AND angle IS VE THEN wheelAngle IS ZE;
	RULE 19 : IF x IS CE AND angle IS LV THEN wheelAngle IS NS;
	RULE 20 : IF x IS CE AND angle IS LU THEN wheelAngle IS NM;
	RULE 21 : IF x IS CE AND angle IS LB THEN wheelAngle IS PB;
	
	RULE 22 : IF x IS RC AND angle IS RB THEN wheelAngle IS PB;
	RULE 23 : IF x IS RC AND angle IS RU THEN wheelAngle IS PM;
	RULE 24 : IF x IS RC AND angle IS RV THEN wheelAngle IS ZE;
	RULE 25 : IF x IS RC AND angle IS VE THEN wheelAngle IS NS;
	RULE 26 : IF x IS RC AND angle IS LV THEN wheelAngle IS NB;
	RULE 27 : IF x IS RC AND angle IS LU THEN wheelAngle IS NB;
	RULE 28 : IF x IS RC AND angle IS LB THEN wheelAngle IS PB;
	
	RULE 29 : IF x IS RI AND angle IS RB THEN wheelAngle IS PS;
	RULE 30 : IF x IS RI AND angle IS RU THEN wheelAngle IS NS;
	RULE 31 : IF x IS RI AND angle IS RV THEN wheelAngle IS NM;
	RULE 32 : IF x IS RI AND angle IS VE THEN wheelAngle IS NM;
	RULE 33 : IF x IS RI AND angle IS LV THEN wheelAngle IS NB;
	RULE 34 : IF x IS RI AND angle IS LU THEN wheelAngle IS NB;
	RULE 35 : IF x IS RI AND angle IS LB THEN wheelAngle IS NB;

END_RULEBLOCK

END_FUNCTION_BLOCK

